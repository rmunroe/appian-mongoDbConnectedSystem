plugins {
    id 'java'
    id 'groovy'
    id 'org.hidetake.ssh' version '2.10.1'
    id 'org.owasp.dependencycheck' version '9.1.0'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib-compile'
    }
}

dependencies {
    compileOnly 'com.appian:connected-systems-core:1.2.0'
    implementation 'com.appian:connected-systems-client:1.1.0'
    testImplementation 'com.appian:connected-systems-core:1.2.0'

    compileOnly fileTree(dir: 'dependencies/lib-compile', include: '*.jar')
    implementation fileTree(dir: 'dependencies/lib', include: '*.jar')

    implementation 'org.mongodb:mongodb-driver-sync:4.11.2'
    implementation 'org.mongodb:mongodb-crypt:1.8.0', { exclude group: 'org.slf4j', module: 'slf4j-api' }

    // These are found in Appian's classpath (<APPIAN_HOME>/deployment/web.war/WEB-INF/lib)
    compileOnly 'org.apache.logging.log4j:log4j-1.2-api:2.23.1'
    compileOnly 'commons-codec:commons-codec:1.17.0'
    compileOnly 'commons-io:commons-io:2.16.1'
    compileOnly 'commons-lang:commons-lang:2.6'
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    into('META-INF/lib') {
        from(configurations.runtimeClasspath)
    }

    into('src') {
        from(sourceSets.main.allJava)
    }

    manifest {
        attributes("Spring-Context": "*;publish-context:=false")
    }
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


version = new groovy.xml.XmlSlurper().parse(file('src/main/resources/appian-plugin.xml')).'plugin-info'.version


apply plugin: 'org.hidetake.ssh'
remotes {
    appian {
        host = '10.0.0.26'
        user = 'appian'
        password = 'appian'

//        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

task deployToVm {
    group = 'install'
    dependsOn jar

    doLast {
        ssh.run {
            session(remotes.appian) {
                put from: "$projectDir/build/libs/${rootProject.name}-${version}.jar", into: '/opt/appian/23.2/_admin/plugins/'
            }
        }
    }
}
